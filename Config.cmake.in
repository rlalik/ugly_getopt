@PACKAGE_INIT@

set(ugly_getopt_known_comps static shared)
set(ugly_getopt_comp_static NO)
set(ugly_getopt_comp_shared NO)

foreach (ugly_getopt_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (ugly_getopt_comp IN_LIST ugly_getopt_known_comps)
        set(ugly_getopt_comp_${ugly_getopt_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "ugly_getopt does not recognize component `${ugly_getopt_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (ugly_getopt_comp_static AND ugly_getopt_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "ugly_getopt `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(ugly_getopt_static_targets "${CMAKE_CURRENT_LIST_DIR}/ugly_getopt-static-targets.cmake")
set(ugly_getopt_shared_targets "${CMAKE_CURRENT_LIST_DIR}/ugly_getopt-shared-targets.cmake")

macro(ugly_getopt_load_targets type)
    if (NOT EXISTS "${ugly_getopt_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "ugly_getopt `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${ugly_getopt_${type}_targets}")
endmacro()

if (ugly_getopt_comp_static)
    ugly_getopt_load_targets(static)
elseif (ugly_getopt_comp_shared)
    ugly_getopt_load_targets(shared)
elseif (DEFINED ugly_getopt_SHARED_LIBS AND ugly_getopt_SHARED_LIBS)
    ugly_getopt_load_targets(shared)
elseif (DEFINED ugly_getopt_SHARED_LIBS AND NOT ugly_getopt_SHARED_LIBS)
    ugly_getopt_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${ugly_getopt_shared_targets}")
        ugly_getopt_load_targets(shared)
    else ()
        ugly_getopt_load_targets(static)
    endif ()
else ()
    if (EXISTS "${ugly_getopt_static_targets}")
        ugly_getopt_load_targets(static)
    else ()
        ugly_getopt_load_targets(shared)
    endif ()
endif ()

check_required_components(ugly_getopt)
